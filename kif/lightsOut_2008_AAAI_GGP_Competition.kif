;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Lights Out
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role robot)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (step 0))

(init (cell 1 1 off))
(init (cell 1 2 off))
(init (cell 1 3 on))
(init (cell 1 4 off))
(init (cell 1 5 off))

(init (cell 2 1 on))
(init (cell 2 2 off))
(init (cell 2 3 off))
(init (cell 2 4 on))
(init (cell 2 5 on))

(init (cell 3 1 off))
(init (cell 3 2 on))
(init (cell 3 3 off))
(init (cell 3 4 on))
(init (cell 3 5 off))

(init (cell 4 1 off))
(init (cell 4 2 off))
(init (cell 4 3 on))
(init (cell 4 4 on))
(init (cell 4 5 on))

(init (cell 5 1 off))
(init (cell 5 2 off))
(init (cell 5 3 on))
(init (cell 5 4 off))
(init (cell 5 5 off))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal robot (press ?x ?y))
	(index ?x)
	(index ?y))
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (cell ?x ?y on))
	(true (cell ?x ?y off))
	(affected ?x ?y))
(<= (next (cell ?x ?y off))
	(true (cell ?x ?y on))
	(affected ?x ?y))

(<= (next (cell ?x ?y ?state))
	(true (cell ?x ?y ?state))
	(not (affected ?x ?y)))

(<= (next (step ?newStep))
	(true (step ?oldStep))
	(succ ?oldStep ?newStep))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
	(true (step 20)))
(<= terminal 
	(not lightsOn))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal robot 100)
	(not lightsOn))
(<= (goal robot 0)
	lightsOn)
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= lightsOn
	(index ?x)
	(index ?y)
	(true (cell ?x ?y on)))

(<= (affected ?x ?y)
	(does robot (press ?x ?y)))
(<= (affected ?x2 ?y)
	(does robot (press ?x1 ?y))
	(plus ?x1 ?x2))
(<= (affected ?x2 ?y)
	(does robot (press ?x1 ?y))
	(plus ?x2 ?x1))
(<= (affected ?x ?y2)
	(does robot (press ?x ?y1))
	(plus ?y1 ?y2))
(<= (affected ?x ?y2)
	(does robot (press ?x ?y1))
	(plus ?y2 ?y1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4) (index 5)
(plus 1 2) (plus 2 3) (plus 3 4) (plus 4 5)

(succ 0 1) (succ 1 2) (succ 2 3) (succ 3 4) (succ 4 5) (succ 5 6) (succ 6 7) (succ 7 8) (succ 8 9) (succ 9 10) (succ 10 11)
(succ 11 12) (succ 12 13) (succ 13 14) (succ 14 15) (succ 15 16) (succ 16 17) (succ 17 18) (succ 18 19) (succ 19 20)
