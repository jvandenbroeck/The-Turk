;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Snake (4x4)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role snake)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (init (cell ?i 1 1 snake))
	(index ?i))

(init (stepCount 0))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal snake (up ?i))
	(true (cell ?i ?x 1 snake)))
(<= (legal snake (up ?i))
	(true (cell ?i ?x 2 snake)))

(<= (legal snake (down ?i))
	(true (cell ?i ?x 2 snake)))
(<= (legal snake (down ?i))
	(true (cell ?i ?x 3 snake)))

(<= (legal snake (right ?i))
	(true (cell ?i 1 ?y snake)))
(<= (legal snake (right ?i))
	(true (cell ?i 2 ?y snake)))

(<= (legal snake (left ?i))
	(true (cell ?i 2 ?y snake)))
(<= (legal snake (left ?i))
	(true (cell ?i 3 ?y snake)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (stepCount ?newCount))
	(true (stepCount ?oldCount))
	(plus 1 ?oldCount ?newCount))

(<= (next (cell ?i ?x ?y body))
	(true (cell ?i ?x ?y body)))

(<= (next (cell ?i ?x ?y body))
	(true (cell ?i ?x ?y snake))
	(moves ?i))
(<= (next (cell ?i ?x ?y snake))
	(true (cell ?i ?x ?y snake))
	(not (moves ?i)))

(<= (next (cell ?i ?x ?y2 snake))
	(true (cell ?i ?x ?y1 snake))
	(does snake (up ?i))
	(plus 1 ?y1 ?y2))
(<= (next (cell ?i ?x ?y2 snake))
	(true (cell ?i ?x ?y1 snake))
	(does snake (down ?i))
	(plus 1 ?y2 ?y1))
(<= (next (cell ?i ?x2 ?y snake))
	(true (cell ?i ?x1 ?y snake))
	(does snake (right ?i))
	(plus 1 ?x1 ?x2))
(<= (next (cell ?i ?x2 ?y snake))
	(true (cell ?i ?x1 ?y snake))
	(does snake (left ?i))
	(plus 1 ?x2 ?x1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
	(index ?i)
	(collision ?i))
(<= terminal
	(true (stepCount 10)))
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal snake 100)
	(index ?i)
	(not (open ?i)))
(<= (goal snake 0)
	(not (goal snake 100)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (collision ?i)
	(true (cell ?i ?x ?y snake))
	(true (cell ?i ?x ?y body)))
	
(<= (open ?i)
	(index ?i)
	(x ?x)
	(y ?y)	
	(not (true (cell ?i ?x ?y snake)))
	(not (true (cell ?i ?x ?y body))))

(<= (moves ?i)
	(does snake (up ?i)))
(<= (moves ?i)
	(does snake (down ?i)))
(<= (moves ?i)
	(does snake (left ?i)))
(<= (moves ?i)
	(does snake (right ?i)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 1) (index 2) (index 3) (index 4) (index 5) (index 6) (index 7) (index 8) (index 9) (index 10) (index 11) (index 12) (index 13) (index 14) (index 15) (index 16)
(x 1) (x 2) (x 3)
(y 1) (y 2) (y 3)
(dir up) (dir down) (dir left) (dir right)

(plus 1  0  1)
(plus 1  1  2)
(plus 1  2  3)
(plus 1  3  4)
(plus 1  4  5)
(plus 1  5  6)
(plus 1  6  7)
(plus 1  7  8)
(plus 1  8  9)
(plus 1  9 10)
